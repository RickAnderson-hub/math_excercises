plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.moowork.node' version '1.3.1'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.pdfbox:pdfbox:3.0.3'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

node {
    version = '14.17.0'
    npmVersion = '6.14.13'
    download = true
    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")
}

group = 'org.rick'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

task npmInstall(type: NpmTask) {
    args = ['install']
    workingDir = file('frontend')
}

task buildFrontend(type: NpmTask, dependsOn: npmInstall) {
    args = ['run', 'build']
    workingDir = file('frontend')
}

processResources {
    dependsOn buildFrontend
    from("frontend/build") {
        into 'static'
    }
}